plugins {
	id "java"
	id "idea"
	id "com.github.andygoossens.modernizer" version "1.12.0"
	id "com.gorylenko.gradle-git-properties" version "2.5.4"
	id "org.springframework.boot" version "3.5.9"
	id "io.spring.dependency-management" version "1.1.7"
	id "com.github.ben-manes.versions" version "0.53.0"
}

group = "de.tum.cit.aet.thesis"
version = "4.7.0"
description = "Thesis Management"

gitProperties {
    dotGitDirectory = file("../.git")
}

springBoot {
	mainClass = "de.tum.cit.aet.thesis.ThesisManagementApplication"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-mail"
	implementation "org.springframework.boot:spring-boot-starter-webflux"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation "org.springframework.boot:spring-boot-starter-actuator"

	developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "org.hibernate.orm:hibernate-core"
    implementation "org.hibernate.orm:hibernate-envers"
	
	// Avoid outdated version to prevent security issues
	implementation("net.minidev:json-smart") { version { 				strictly "2.6.0" } }

	implementation "org.liquibase:liquibase-core:4.33.0"
	implementation "org.postgresql:postgresql:42.7.8"

	implementation "commons-io:commons-io:2.21.0"
	implementation "com.github.vladimir-bukhtoyarov:bucket4j-core:8.0.1"
	implementation "org.mnode.ical4j:ical4j:4.2.2"
	implementation "com.itextpdf:itext-core:9.4.0"
	implementation "com.itextpdf:html2pdf:6.3.0"
	implementation "com.auth0:java-jwt:4.5.0"

	// Use the latest version of commons-lang3 to avoid security issues
	implementation "org.apache.commons:commons-lang3:${commons_lang3_version}"

	implementation "ch.qos.logback:logback-classic:"
	implementation "ch.qos.logback:logback-core:"

	// use newest version of commons-compress to avoid security issues through outdated dependencies
	implementation "org.apache.commons:commons-compress:1.28.0"

	compileOnly "org.projectlombok:lombok:1.18.42"
	annotationProcessor "org.projectlombok:lombok:1.18.42"

	testImplementation("org.springframework.boot:spring-boot-starter-test") {
		exclude group: "org.junit.vintage", module: "junit-vintage-engine"
		exclude group: "com.vaadin.external.google", module: "android-json"
		exclude group: "org.xmlunit", module: "xmlunit-core"
	}

	testImplementation "org.mockito:mockito-core:5.21.0"
	testImplementation "org.mockito:mockito-junit-jupiter:5.21.0"

	testImplementation "org.testcontainers:testcontainers:${test_container_version}"
	testImplementation "org.testcontainers:junit-jupiter:${test_container_version}"
	testImplementation "org.testcontainers:jdbc:${test_container_version}"
	testImplementation "org.testcontainers:database-commons:${test_container_version}"
	testImplementation "org.testcontainers:postgresql:${test_container_version}"

	testImplementation "org.junit.jupiter:junit-jupiter:${junit_version}"
	testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
	testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"

	// NOTE: make sure this corresponds to the version used for JUnit in the testImplementation
	testImplementation "org.junit.platform:junit-platform-commons:${junit_version}"
	testImplementation "org.junit.platform:junit-platform-engine:${junit_version}"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_version}"
}

springBoot {
	buildInfo()
}

test {
	useJUnitPlatform()

	maxParallelForks = 1
	forkEvery = 0

	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
	}
	testLogging.showStandardStreams = true
	systemProperty "spring.profiles.active", "test"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
    options.compilerArgs << "-parameters"
}

def isNonStable = { String version ->
	def stableKeyword = ["RELEASE", "FINAL", "GA"].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
	rejectVersionIf {
		isNonStable(it.candidate.version)
	}

	rejectVersionIf {
		isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
	}
}

// Available commands:
//
// Find dependency updates:    ./gradlew dependencyUpdates -Drevision=release
// Test:                       ./gradlew test
// Build:                      ./gradlew build -x test
// Run:						   ./gradlew bootRun
